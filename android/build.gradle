buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        androidTest {
            java.srcDirs = ['src/androidTest/java', project(':tests').file('src/test/java')]
        }
    }
}

repositories {
    google()
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "com.github.getlantern.curve25519-java:curve25519-android:4a5f50748f"
    implementation (project(':java')) {
        exclude group: 'com.github.getlantern.curve25519-java', module: 'curve25519-java'
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('sonatypeRepo') ? sonatypeRepo
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getRepositoryUsername() {
    return hasProperty('whisperSonatypeUsername') ? whisperSonatypeUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('whisperSonatypePassword') ? whisperSonatypePassword : ""
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

// the below is needed in order to get the archive to publish with maven/Jitpack
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId    = group_info
                artifactId = "signal-protocol-android"
                version    = version_number
            }
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug
                groupId    = group_info
                artifactId = "signal-protocol-android"
                version    = version_number
            }
        }
    }
}